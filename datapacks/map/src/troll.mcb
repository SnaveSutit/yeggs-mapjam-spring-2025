
function on_load minecraft:load {
	bossbar add troll:satisfaction {text:"Satisfaction",color:yellow}
	bossbar add troll:patience {text:"His Immenseness' Patience", color:red}
}

function reset {
	bossbar set troll:satisfaction players
	bossbar set troll:satisfaction style notched_20
	bossbar set troll:satisfaction max 20
	bossbar set troll:satisfaction value 0
	bossbar set troll:satisfaction color yellow

	bossbar set troll:patience players
	bossbar set troll:patience style notched_6
	bossbar set troll:patience max 6
	bossbar set troll:patience value 6
	bossbar set troll:patience color red

	stopsound @a record minecraft:music.1
	stopsound @a record minecraft:music.2
	scoreboard players set #music_mode i 0
	scoreboard players set #music_timer i 0

	schedule clear troll:consume
}


function on_tick minecraft:tick {
	execute if score #game_in_progress i matches 1 run scoreboard players add #music_timer i 1
	execute if score #music_mode i matches 0 if score #music_timer i matches 2400.. run {
		playsound minecraft:music.2 record @a 0 0 0 100 1
		scoreboard players set #music_timer i 0
	}
	execute if score #music_mode i matches 1 if score #music_timer i matches 1800.. run {
		playsound minecraft:music.1 record @a 0 0 0 100 1
		scoreboard players set #music_timer i 0
	}
}


function summon {
	execute positioned 0 1 -11 rotated 0 0 run function animated_java:troll/summon {args:{animation:idle,start_animation:true}}
	execute positioned 0 7 2 rotated 180 -20 summon minecraft:item_display run {
		tag @s add death_anim_player_lock
		tp @s ~ ~ ~ ~ ~
	}
}


function begin_consuming {
	function troll:reset
	playsound minecraft:music.2 record @a 0 0 0 100 1
	scoreboard players set #music_mode i 0
	scoreboard players set #music_timer i 0
	bossbar set troll:satisfaction players @a
	bossbar set troll:patience players @a
	scoreboard players set #troll_patience i 6
	scoreboard players set #satisfaction i 0
	schedule function troll:consume 60s
}

function consume {
	scoreboard players set #satisfied i 0
	execute positioned 0 4 0 run {
		execute as @n[tag=layer,distance=..3] on passengers on passengers if entity @s[tag=aj.layer.root] run {
			function animated_java:layer/animations/monch/play
			# TODO: move this to an animation to sync better
			playsound minecraft:entity.generic.eat block @a 0 3 -11 10 0.1
			scoreboard players set #satisfied i 1
			scoreboard players set #troll_patience i 6
			scoreboard players add #satisfaction i 1
			execute store result bossbar troll:satisfaction value run scoreboard players get #satisfaction i
		}
	}
	execute if score #satisfaction i matches 20 run {
		tellraw @a ["<", {text:"His Immenseness William Cornelius Grumblethorn III",color:green}, "> ", {text:"Yum! That was delicious!","color":"yellow"}]
		playsound minecraft:entity.villager.yes block @a ~ ~ ~ 10 0.1
		schedule clear troll:consume
		function map:win
	}
	execute if score #satisfied i matches 1 run {
		schedule function troll:consume 60s
	}
	execute if score #satisfied i matches 0 run {
		function *get_mad
	}
	execute store result bossbar troll:patience value run scoreboard players get #troll_patience i
}

function get_mad {
	scoreboard players remove #troll_patience i 1
	execute if score #troll_patience i matches 1.. run {
		execute store result score #random i run random value 0..5
		execute if score #random i matches 0 run tellraw @a ["<", {text:"His Immenseness William Cornelius Grumblethorn III",color:green}, "> ", {text:"You are taking too long!",color:red}]
		execute if score #random i matches 1 run tellraw @a ["<", {text:"His Immenseness William Cornelius Grumblethorn III",color:green}, "> ", {text:"I ain't got nothin' to eat!",color:red}]
		execute if score #random i matches 2 run tellraw @a ["<", {text:"His Immenseness William Cornelius Grumblethorn III",color:green}, "> ", {text:"I am starving!",color:red}]
		execute if score #random i matches 3 run tellraw @a ["<", {text:"His Immenseness William Cornelius Grumblethorn III",color:green}, "> ", {text:"Are you even trying?",color:red}]
		execute if score #random i matches 4 run tellraw @a ["<", {text:"His Immenseness William Cornelius Grumblethorn III",color:green}, "> ", {text:"Stop slacking and get me my cake!",color:red}]
		execute if score #random i matches 5 run tellraw @a ["<", {text:"His Immenseness William Cornelius Grumblethorn III",color:green}, "> ", {text:"Where's my cake?!",color:red}]
		playsound minecraft:entity.villager.ambient block @a ~ ~ ~ 10 0.1
		schedule function troll:consume 60s
	}
	execute if score #troll_patience i matches 2 run {
		stopsound @a record minecraft:music.1
		stopsound @a record minecraft:music.2
		playsound minecraft:music.1 record @a 0 0 0 100 1
		scoreboard players set #music_mode i 1
		scoreboard players set #music_timer i 0
	}
	execute if score #troll_patience i matches ..0 run {
		function *game_over
	}
}

function game_over {
	tellraw @a ["<", {text:"His Immenseness William Cornelius Grumblethorn III",color:green}, "> ", {"text":"That's it! If you can't make my cake fast enough, I'll just be eatin' you instead!","color":"red"}]
	schedule clear troll:consume

	stopsound @a record minecraft:music.1
	stopsound @a record minecraft:music.2

	gamemode spectator @a
	execute as @a run spectate @n[type=item_display,tag=death_anim_player_lock] @s
	# TODO: add a death animation
	schedule function map:game_over 5s
}