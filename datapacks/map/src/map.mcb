function on_load minecraft:load {
	gamerule maxCommandChainLength 1000000

	scoreboard objectives add mcb.i dummy

	scoreboard objectives add i dummy

	scoreboard players set 2 i 2
	scoreboard players set 4 i 4
	scoreboard players set 7 i 7
	scoreboard players set 8 i 8
	scoreboard players set 10 i 10
	scoreboard players set 20 i 20
	scoreboard players set 60 i 60
	scoreboard players set 100 i 100
	scoreboard players set 360 i 360

	scoreboard objectives add spawn_timer dummy

	setworldspawn 0 35 41 90

	scoreboard players set #current_tick i 0

	tellraw @a {"text":"Map reloaded!","color":"green"}

	# schedule function map:reset 10t
}

function start {
	scoreboard players set #game_in_progress i 1
	tellraw @a {text:'Starting Game...',color:green}
	scoreboard players set #playtime i 0
	execute as @a run {
		function player:join_players
		function map:teleport_to_game
	}
	function troll:begin_consuming
}


function stop {
	scoreboard players set #game_in_progress i 0
	function map:reset
}


function on_tick minecraft:tick {
	scoreboard players add #current_tick i 1
	scoreboard players add #playtime i 1
	# Tutorial Button
	execute if block -5 35 43 minecraft:birch_button[powered=true] run {
		function map:hide_buttons
		function tutorial:start
	}
	# Play Button
	execute if block -8 35 43 minecraft:birch_button[powered=true] run {
		function map:hide_buttons
		function map:start
	}
	# Reset Button
	execute if block -21 36 43 minecraft:birch_button[powered=true] run {
		function map:hide_buttons
		function map:reset
	}
}


function hide_buttons {
	setblock -5 35 43 air
	setblock -8 35 43 air
	setblock -21 36 43 air
}


function show_buttons {
	setblock -5 35 43 minecraft:birch_button[powered=false]
	setblock -8 35 43 minecraft:birch_button[powered=false]
	setblock -21 36 43 minecraft:birch_button[powered=false]
}


function set_scale {
	#ARGS: {scale: int}
	attribute @s minecraft:scale modifier remove scaled
	$attribute @s minecraft:scale modifier add scaled $(scale) add_multiplied_base

	attribute @s minecraft:jump_strength modifier remove scaled
	$attribute @s minecraft:jump_strength modifier add scaled $(scale) add_multiplied_base

	attribute @s minecraft:gravity modifier remove scaled
	$attribute @s minecraft:gravity modifier add scaled $(scale) add_multiplied_base

	attribute @s minecraft:movement_speed modifier remove scaled
	$attribute @s minecraft:movement_speed modifier add scaled $(scale) add_multiplied_base

	attribute @s minecraft:step_height modifier remove scaled
	$attribute @s minecraft:step_height modifier add scaled $(scale) add_multiplied_base

	attribute @s minecraft:safe_fall_distance modifier remove scaled
	$attribute @s minecraft:safe_fall_distance modifier add scaled $(scale) add_multiplied_base
}


function reset {
	tellraw @a {text:'Resetting Map...',color:yellow}
	function map:hide_buttons
	scoreboard players set #game_in_progress i 0
	kill @e[type=!player]
	function static_pipette:remove
	schedule 1s {
		function tutorial:clear_schedules
		# Lobby
		summon minecraft:text_display -5.9375 37.0625 43.9375 {Passengers: [{alignment: "center", background: 0, default_background: 0b, fall_distance: 0.0d, id: "minecraft:text_display", line_width: 200, see_through: 0b, shadow: 0b, text: {italic: 1b, text: "Welcome to"}, text_opacity: -1b, transformation: {left_rotation: [0.0f, 1.0f, 0.0f, 0.0f], right_rotation: [0.0f, 0.0f, 0.0f, 1.0f], scale: [1.6875f, 1.6874992f, 1.6874995f], translation: [0.0f, 1.0f, -0.0625f]}}, {alignment: "center", background: 0, default_background: 0b, fall_distance: 0.0d, id: "minecraft:text_display", line_width: 200, see_through: 0b, shadow: 1b, text: {color: "green", text: "Play!"}, text_opacity: -1b, transformation: {left_rotation: [0.0f, 1.0f, 0.0f, 0.0f], right_rotation: [0.0f, 0.0f, 0.0f, 1.0f], scale: [1.1875f, 1.1875f, 1.1875f], translation: [-1.5625f, -0.9375f, 0.0f]}}, {alignment: "center", background: 0, default_background: 0b, fall_distance: 0.0d, id: "minecraft:text_display", line_width: 200, see_through: 0b, shadow: 1b, text: {color: "aqua", text: "Tutorial"}, text_opacity: -1b, transformation: {left_rotation: [0.0f, 1.0f, 0.0f, 0.0f], right_rotation: [0.0f, 0.0f, 0.0f, 1.0f], scale: [1.1875f, 1.1875f, 1.1875f], translation: [1.4375f, -0.9375f, 0.0f]}}], alignment: "center", background: 0, default_background: 0b, fall_distance: 0.0d, line_width: 200, see_through: 0b, shadow: 1b, text: {color: "green", extra: [{bold: 1b, color: "white", text: "Pastry "}, {bold: 1b, color: "light_purple", italic: 1b, text: "Panic!"}], text: "Pocket-sized "}, text_opacity: -1b, transformation: {left_rotation: [0.0f, 1.0f, 0.0f, 0.0f], right_rotation: [0.0f, 0.0f, 0.0f, 1.0f], scale: [3.5625f, 3.5624998f, 3.5624995f], translation: [0.0f, 0.0f, 0.0f]}}
		summon minecraft:text_display -20.5 37.125 43.9375 {alignment: "center", background: 0, default_background: 0b, fall_distance: 0.0d, line_width: 200, see_through: 0b, shadow: 1b, text: {color: "red", text: "Reset Map"}, text_opacity: -1b, transformation: {left_rotation: [0.0f, 1.0f, 0.0f, 0.0f], right_rotation: [0.0f, 0.0f, 0.0f, 1.0f], scale: [1.0f, 1.0f, 1.0f], translation: [0.0f, 0.0f, 0.0f]}}
		summon minecraft:text_display -22.0 34.5 40.75 {alignment: "center", background: 0, default_background: 0b, fall_distance: 0.0d, line_width: 180, see_through: 0b, shadow: 1b, text: {color: "gray", extra: [{color: "red", text: "\nSnaveSutit"}, " & ", {color: "green", text: "XelaFella"}, {color: "gray", italic: 1b, text: "\nas "}, {bold: 1b, color: "blue", text: "Team ADHD"}, {color: "gray", italic: 1b, text: "\n\nCreated during the 96 hour Yeggs Spring MapJam of 2025!"}], text: "Created by "}, text_opacity: -1b, transformation: {left_rotation: [0.0f, 0.7071068f, 0.0f, 0.7071068f], right_rotation: [0.0f, 0.0f, 0.0f, 1.0f], scale: [0.9999997f, 0.99999994f, 0.9999997f], translation: [0.0f, 0.0f, 0.0f]}}
		# Decoration Ingredients
		execute positioned 23 23 40 run function ingredient:summon_random
		execute positioned 15 23 38 run function ingredient:summon_random
		execute positioned 9 22 38 run function ingredient:summon_random
		execute positioned 5 22 41 run function ingredient:summon_random
		execute positioned 18 30 41 run function ingredient:summon_random
		execute positioned 6 31 39 run function ingredient:summon_random
		execute positioned -5 25 40 run function ingredient:summon_random
		execute positioned -21 26 40 run function ingredient:summon_random
		execute positioned 5 31 39 run function ingredient:summon_random

		function troll:summon
		function troll:reset
		function orders:summon
		# Forklift
		execute positioned -20 1 -18 rotated 180 0 run function forklift:summon
		# Pan Dispenser
		execute positioned -18 2 -34 run function pan:create_dispenser
		# Ovens
		execute positioned -35 1 -16 rotated -90 0 run function ovens:summon
		execute positioned -35 1 0 rotated -90 0 run function ovens:summon
		execute positioned -35 1 16 rotated -90 0 run function ovens:summon
		# Mixing Stations
		execute positioned 34.0 6 -19.0 rotated 90 0 run function mixer:summon
		execute positioned 34.0 6 -2.0 rotated 90 0 run function mixer:summon
		# Frosting Mixers
		execute positioned 13 1 31 rotated 180 0 run function frosting_mixer:summon
		execute positioned -13 1 31 rotated 180 0 run function frosting_mixer:summon
		# Frosting Station
		# function static_pipette:summon
		execute positioned 34 3 10 run function static_pipette:create_lock_point
		# Cake Launcher
		execute positioned 0 1 13 rotated 0 0 run function launcher:summon
		# Ingredients
		execute positioned 2 4 -37 rotated 0 0 run function ingredient:create_dispenser {type:milk}
		execute positioned 8 4 -37 rotated 0 0 run function ingredient:create_dispenser {type:milk}
		execute positioned 14 4 -37 rotated 0 0 run function ingredient:create_dispenser {type:eggs}
		execute positioned 8 12 -37 rotated 0 0 run function ingredient:create_dispenser {type:flour}
		execute positioned 14 12 -37 rotated 0 0 run function ingredient:create_dispenser {type:sugar}
		execute positioned 20 12 -37 rotated 0 0 run function ingredient:create_dispenser {type:sugar}
		execute positioned 2 20 -37 rotated 0 0 run function ingredient:create_dispenser {type:vanilla}
		execute positioned 8 20 -37 rotated 0 0 run function ingredient:create_dispenser {type:chocolate}
		execute positioned 20 20 -37 rotated 0 0 run function ingredient:create_dispenser {type:berries}
		# Fan
		execute positioned 37 -3 -11 run function map:fan/summon
		# Misc
		function sequence:reset_sequence
		execute as @e[type=minecraft:item_display,nbt={item:{}}] run data modify entity @s view_range set value 2

		function map:show_buttons
		execute as @a[tag=!dev] run {
			function map:teleport_to_spawn
			function player:join_players
		}

		tellraw @a {text:'Map reset successfully!',color:green}
	}
}


dir fan {
	function summon {
		execute rotated 0 0 run function animated_java:fan/summon {args:{animation:"spin",start_animation:true}}
	}
	function on_tick minecraft:tick {
		execute as @e[type=minecraft:item_display,tag=fan] at @s positioned ~-1 ~ ~-1 run {
			particle minecraft:white_smoke ~1 ~5 ~1 0.5 3 0.5 0 1

			execute as @a[dx=2,dy=15,dz=2] run effect give @s minecraft:levitation 1 30 true
			execute positioned ~-4 ~18 ~-4 as @a[dx=5,dy=2,dz=5] run effect clear @s minecraft:levitation

			execute as @e[type=minecraft:armor_stand,tag=ingredient,dx=2,dy=15,dz=2] run effect give @s minecraft:levitation 1 30 true
			execute positioned ~-4 ~18 ~-4 as @e[type=minecraft:armor_stand,tag=ingredient,dx=5,dy=2,dz=5] run effect clear @s minecraft:levitation
		}
	}
}

function teleport_to_spawn {
	tp @s 0 35 41 90 0
}


function teleport_to_game {
	tp @s 0 1 25 180 -10
}


function game_over {
	title @a times 10 80 10
	title @a title ["",{text:"\ua000",font:"cooking:custom"},{translate:"space.-1"},{text:"\ua000",font:"cooking:custom"},{translate:offset.-256,with:[{text:"Game Over!",color:red}]}]

	scoreboard players set #game_in_progress i 0

	execute as @a at @s run {
		playsound minecraft:entity.generic.eat block @s ~ ~ ~ 10 0.1
		playsound minecraft:item.goat_horn.sound.6 block @a ~ ~ ~ 0.8 1
	}
	schedule 50t replace {
		function map:reset
	}
	schedule 80t replace {
		function map:teleport_to_spawn
		gamemode adventure @a
		function map:show_buttons
	}
}

function win {
	title @a times 10 80 10
	title @a title ["",{text:"\ua000",font:"cooking:custom"},{translate:"space.-1"},{text:"\ua000",font:"cooking:custom"},{translate:offset.-256,with:[{text:"You Win!",color:green}]}]
	schedule 80t replace {
		function map:display_score
		execute as @a run function map:teleport_to_spawn
		function map:reset
	}
}

function display_score {
	scoreboard players operation #seconds i = #playtime i
	scoreboard players operation #seconds i /= 20 i
	scoreboard players operation #minutes i = #seconds i
	scoreboard players operation #seconds i %= 60 i
	scoreboard players operation #seconds i %= 60 i
	scoreboard players operation #minutes i /= 60 i

	execute store result score #leftovers i if entity @e[tag=layer]

	scoreboard players operation #total_cakes i = #required_satisfaction i
	scoreboard players operation #total_cakes i += #leftovers i

	title @a subtitle [{text:"You launched ",color:green}, {score:{name:'#total_cakes',objective:'i'},color:aqua}, {text:" cakes in "}, {score:{name:'#minutes',objective:'i'},color:aqua}, {text:" minutes and "}, {score:{name:'#seconds',objective:'i'},color:aqua}, {text:" seconds!"}]

	schedule 80t replace {
		tellraw @a [{text:'---- You Win! ----\n',color:green}, {text:"You launched "}, {score:{name:'#total_cakes',objective:'i'},color:aqua}, {text:" cakes in "}, {score:{name:'#minutes',objective:'i'},color:aqua}, {text:" minutes and "}, {score:{name:'#seconds',objective:'i'},color:aqua}, {text:" seconds!"},'\n----------------']
	}
}