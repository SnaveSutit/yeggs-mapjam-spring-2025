function on_load minecraft:load {
	gamerule maxCommandChainLength 1000000

	scoreboard objectives add mcb.i dummy

	scoreboard objectives add i dummy

	scoreboard players set 2 i 2
	scoreboard players set 4 i 4
	scoreboard players set 7 i 7
	scoreboard players set 8 i 8
	scoreboard players set 10 i 10
	scoreboard players set 100 i 100
	scoreboard players set 360 i 360

	scoreboard objectives add spawn_timer dummy

	setworldspawn 0 35 41 90

	tellraw @a {"text":"Map reloaded!","color":"green"}

	# schedule function map:reset 10t
}

function start {
	scoreboard players set #game_in_progress i 1
}


function set_scale {
	#ARGS: {scale: int}
	attribute @s minecraft:scale modifier remove scaled
	$attribute @s minecraft:scale modifier add scaled $(scale) add_multiplied_base

	attribute @s minecraft:jump_strength modifier remove scaled
	$attribute @s minecraft:jump_strength modifier add scaled $(scale) add_multiplied_base

	attribute @s minecraft:gravity modifier remove scaled
	$attribute @s minecraft:gravity modifier add scaled $(scale) add_multiplied_base

	attribute @s minecraft:movement_speed modifier remove scaled
	$attribute @s minecraft:movement_speed modifier add scaled $(scale) add_multiplied_base

	attribute @s minecraft:step_height modifier remove scaled
	$attribute @s minecraft:step_height modifier add scaled $(scale) add_multiplied_base

	attribute @s minecraft:safe_fall_distance modifier remove scaled
	$attribute @s minecraft:safe_fall_distance modifier add scaled $(scale) add_multiplied_base
}

function reset {
	scoreboard players set #game_in_progress i 0
	kill @e[type=!player]
	function static_pipette:remove
	schedule 1t {

		# Lobby
		summon minecraft:text_display -5.9375 37.25 43.9375 {Passengers: [{alignment: "center", background: 1073741824, default_background: 0b, fall_distance: 0.0d, id: "minecraft:text_display", line_width: 200, see_through: 0b, shadow: 0b, text: "Welcome to", text_opacity: -1b, transformation: {left_rotation: [0.0f, 1.0f, 0.0f, 0.0f], right_rotation: [0.0f, 0.0f, 0.0f, 1.0f], scale: [1.6875f, 1.6874992f, 1.6874995f], translation: [0.0f, 1.0f, -0.0625f]}}], alignment: "center", background: 1073741824, default_background: 0b, fall_distance: 0.0d, line_width: 200, see_through: 0b, shadow: 1b, text: {color: "green", extra: [{bold: 1b, color: "white", text: "Pastry "}, {bold: 1b, color: "light_purple", italic: 1b, text: "Panic"}], text: "Pocket-sized "}, text_opacity: -1b, transformation: {left_rotation: [0.0f, 1.0f, 0.0f, 0.0f], right_rotation: [0.0f, 0.0f, 0.0f, 1.0f], scale: [3.5625f, 3.5624998f, 3.5624995f], translation: [0.0f, 0.0f, 0.0f]}}

		function troll:summon
		function troll:reset
		function orders:summon
		# Forklift
		execute positioned -20 1 -18 rotated 180 0 run function forklift:summon
		# Pan Dispenser
		execute positioned -19 2 -34 run function pan:create_dispenser
		# Ovens
		execute positioned -35 1 -16 rotated -90 0 run function ovens:summon
		execute positioned -35 1 0 rotated -90 0 run function ovens:summon
		execute positioned -35 1 16 rotated -90 0 run function ovens:summon
		# Mixing Stations
		execute positioned 34.0 6 -19.0 rotated 90 0 run function mixer:summon
		execute positioned 34.0 6 -2.0 rotated 90 0 run function mixer:summon
		# Frosting Mixers
		execute positioned 13 1 31 rotated 180 0 run function frosting_mixer:summon
		execute positioned -13 1 31 rotated 180 0 run function frosting_mixer:summon
		# Frosting Station
		# function static_pipette:summon
		execute positioned 34 3 10 run function static_pipette:create_lock_point
		# Cake Launcher
		execute positioned 0 1 13 rotated 0 0 run function launcher:summon
		# Ingredients
		execute positioned 2 4 -37 rotated 0 0 run function ingredient:create_dispenser {type:milk}
		execute positioned 8 4 -37 rotated 0 0 run function ingredient:create_dispenser {type:milk}
		execute positioned 14 4 -37 rotated 0 0 run function ingredient:create_dispenser {type:eggs}
		execute positioned 8 12 -37 rotated 0 0 run function ingredient:create_dispenser {type:flour}
		execute positioned 14 12 -37 rotated 0 0 run function ingredient:create_dispenser {type:sugar}
		execute positioned 20 12 -37 rotated 0 0 run function ingredient:create_dispenser {type:sugar}
		execute positioned 2 20 -37 rotated 0 0 run function ingredient:create_dispenser {type:vanilla}
		execute positioned 8 20 -37 rotated 0 0 run function ingredient:create_dispenser {type:chocolate}
		execute positioned 20 20 -37 rotated 0 0 run function ingredient:create_dispenser {type:berries}
		# Fan
		execute positioned 37 -3 -11 run function map:fan/summon
		# Misc
		function sequence:reset_sequence
	}
}

function setup_debug_environment {
	execute unless entity @s[name=SnaveSutit] run return run tellraw @s {"text":"You are not SnaveSutit!","color":"red"}
	kill @e[type=!player]
	# Pan Dispenser
	execute positioned -119 2 -34 run function pan:create_lock_point
	execute positioned -119 4 -34 run function pan:summon
	execute positioned -133 1 -15 rotated -90 0 run function ovens:summon
	execute positioned -119 1 -18 run function forklift:debug
	execute positioned -68.0 5 -16.0 rotated 90 0 run function animated_java:mixer/summon {args:{}}
	execute positioned -68.0 5 1.0 rotated 90 0 run function animated_java:mixer/summon {args:{}}
	# Frosting area
	execute positioned -67 3 24 run function pan:create_lock_point
	# Launcher
	execute positioned -100 1 17 rotated 0 0 run function animated_java:launcher/summon {args:{}}

	# Food items
	execute positioned -108 2 -25 rotated 0 0 run function ingredient:summon {type:milk}
	execute positioned -104 2 -25 rotated 0 0 run function ingredient:summon {type:eggs}
	execute positioned -100 2 -25 rotated 0 0 run function ingredient:summon {type:flour}
	execute positioned -96 2 -25 rotated 0 0 run function ingredient:summon {type:sugar}

	execute positioned -92 2 -25 rotated 0 0 run function ingredient:summon {type:vanilla}
	execute positioned -88 2 -25 rotated 0 0 run function ingredient:summon {type:chocolate}
	execute positioned -84 2 -25 rotated 0 0 run function ingredient:summon {type:berries}
}

dir fan {
	function summon {
		execute rotated 0 0 run function animated_java:fan/summon {args:{animation:"spin",start_animation:true}}
	}
	function on_tick minecraft:tick {
		execute as @e[type=minecraft:item_display,tag=fan] at @s positioned ~-1 ~ ~-1 run {
			particle minecraft:white_smoke ~1 ~5 ~1 0.5 3 0.5 0 1
			execute as @a[dx=2,dy=15,dz=2] run {
				effect give @s minecraft:levitation 1 30 true
			}
			execute positioned ~-4 ~18 ~-4 as @a[dx=5,dy=2,dz=5] run {
				effect clear @s minecraft:levitation
			} 
		}
	}
}

function teleport_to_spawn {
	tp @s 0 35 41 90 0
}

function game_over {
	title @a times 10 80 10
	title @a title ["",{text:"\ua000",font:"cooking:custom"},{translate:"space.-1"},{text:"\ua000",font:"cooking:custom"},{translate:offset.-256,with:[{text:"Game Over!",color:red}]}]
	execute as @a at @s run {
		playsound minecraft:entity.generic.eat block @s ~ ~ ~ 10 0.1
	}
	schedule 50t replace {
		function map:reset
	}
	schedule 80t replace {
		function map:teleport_to_spawn
		gamemode adventure @a
	}
}
