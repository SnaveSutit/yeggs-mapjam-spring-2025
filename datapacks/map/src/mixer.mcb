function on_load minecraft:load {
	scoreboard objectives add mixer_state dummy
	# State 0 = idle / closed
	# State 1 = animating
	# State 2 = open and waiting for ingredients
	# State 3 = closed and waiting for mixing
	# State 4 = mixed and ready to pour
	scoreboard objectives add mixing_needed dummy
}

function summon {
	# -68.0 5 -16.0
	execute rotated 90 0 run function animated_java:mixer/summon {args:{}}
}

dir debug {
	function skip_ingredients {
		function animated_java:mixer/animations/close_lid/play
		scoreboard players set @s mixing_needed 100
		scoreboard players set @s mixer_state 1
	}
}

function clear_recipe_tags {
	tag @s remove contains_milk
	tag @s remove contains_flour
	tag @s remove contains_sugar
	tag @s remove contains_eggs
	tag @s remove contains_vanilla
	tag @s remove contains_chocolate
	tag @s remove contains_berries
}

function clear_batter_tags {
	tag @s remove vanilla_batter
	tag @s remove chocolate_batter
	tag @s remove berry_batter
}

function on_tick minecraft:tick {
	execute as @e[type=minecraft:item_display,tag=aj.mixer.root] at @s run {

		execute if score @s mixer_state matches 0 run {
			function animated_java:mixer/animations/open_lid/play
			scoreboard players set @s mixer_state 1
		}

		execute if score @s mixer_state matches 2 run {
			scoreboard players set #ingredient_type i -1
			tag @s add this_mixer
			execute positioned ~-5 ~ ~-5 as @e[type=minecraft:armor_stand,tag=ingredient,dx=10,dy=0,dz=10] run {
				execute if entity @s[tag=ingredient_milk] run scoreboard players set #ingredient_type i 0
				execute if entity @s[tag=ingredient_flour] run scoreboard players set #ingredient_type i 1
				execute if entity @s[tag=ingredient_sugar] run scoreboard players set #ingredient_type i 2
				execute if entity @s[tag=ingredient_eggs] run scoreboard players set #ingredient_type i 3
				execute if entity @s[tag=ingredient_vanilla] run scoreboard players set #ingredient_type i 4
				execute if entity @s[tag=ingredient_chocolate] run scoreboard players set #ingredient_type i 5
				execute if entity @s[tag=ingredient_berries] run scoreboard players set #ingredient_type i 6
				execute at @s run playsound minecraft:entity.generic.splash block @a ~ ~ ~ 2 1
				function util:kill_stacked_entities
			}
			tag @s remove this_mixer
			execute if score #ingredient_type i matches 0.. run {
				say Adding ingredient
				execute if score #ingredient_type i matches 0 run tag @s add contains_milk
				execute if score #ingredient_type i matches 1 run tag @s add contains_flour
				execute if score #ingredient_type i matches 2 run tag @s add contains_sugar
				execute if score #ingredient_type i matches 3 run tag @s add contains_eggs
				execute if score #ingredient_type i matches 4 run tag @s add contains_vanilla
				execute if score #ingredient_type i matches 5 run tag @s add contains_chocolate
				execute if score #ingredient_type i matches 6 run tag @s add contains_berries
				# Check for complete recipe
				execute if entity @s[tag=contains_milk,tag=contains_flour,tag=contains_sugar,tag=contains_eggs] run {
					say Checking for complete recipe
					execute if entity @s[tag=contains_vanilla] run {
						say "Vanilla recipe!"
						function animated_java:mixer/animations/close_lid/play
						scoreboard players set @s mixing_needed 100
						scoreboard players set @s mixer_state 1
						function mixer:clear_recipe_tags
						tag @s add vanilla_batter
					}
					execute if entity @s[tag=contains_chocolate] run {
						say "Chocolate recipe!"
						function animated_java:mixer/animations/close_lid/play
						scoreboard players set @s mixing_needed 100
						scoreboard players set @s mixer_state 1
						function mixer:clear_recipe_tags
						tag @s add chocolate_batter
					}
					execute if entity @s[tag=contains_berries] run {
						say "Berry recipe!"
						function animated_java:mixer/animations/close_lid/play
						scoreboard players set @s mixing_needed 100
						scoreboard players set @s mixer_state 1
						function mixer:clear_recipe_tags
						tag @s add berry_batter
					}
				}
			}
		}

		execute if score @s mixer_state matches 3 run {
			scoreboard players set #check i 0
			execute on passengers if entity @s[tag=aj.global.data] run { with entity @s item.components."minecraft:custom_data".locators.handle_zone
				$execute positioned ^$(posx) ^$(posy) ^$(posz) rotated ~$(roty) ~$(rotx) run {
					execute if entity @a[distance=..2] run scoreboard players set #check i 1
				}
			}
			execute if score #check i matches 1 run {
				function animated_java:mixer/animations/mix/resume
				scoreboard players remove @s mixing_needed 1
				REPEAT(0, 100) as i {
					execute if score @s mixing_needed matches <%i%> run return run playsound minecraft:block.wooden_button.click_on block @a ~ ~ ~ 1 <%0.8 + 1 * ((101 - i) / 100)%>
				}
			}
			execute if score #check i matches 0 run {
				function animated_java:mixer/animations/mix/pause
			}
			execute if score @s mixing_needed matches ..0 run {
				function animated_java:mixer/animations/mix/pause
				scoreboard players set @s aj.mix.frame 0
				title @a times 10 40 10
				title @a title [{"text":"Mixing Complete!","color":"green"}]
				playsound minecraft:block.note_block.bell block @a ~ ~ ~ 2
				scoreboard players set @s mixer_state 4
			}
		}
	}
}

function on_interaction {
	execute if entity @s[tag=mixer_valve] run return run function *valve/on_interaction
}

function fill_lid {
	fill ~5.5 ~9 ~5.5 ~-5.5 ~9 ~-5.5 minecraft:barrier
}

function clear_lid {
	fill ~5.5 ~9 ~5.5 ~-5.5 ~9 ~-5.5 minecraft:air
}

dir valve {
	function on_interaction {
		scoreboard players operation #this aj.id = @s aj.id
		execute as @e[type=minecraft:item_display,tag=aj.mixer.root] if score @s aj.id = #this aj.id run {
			execute if score @s mixer_state matches 4 run {
				function animated_java:mixer/animations/pour/play
				scoreboard players set @s mixer_state 1
			}
		}
	}

	function fill_pan {
		execute on passengers if entity @s[tag=aj.global.data] run { with entity @s item.components."minecraft:custom_data".locators.spout_output
			$execute positioned ^$(posx) ^$(posy) ^$(posz) rotated ~$(roty) ~$(rotx) run {
				particle end_rod ~ ~ ~
				scoreboard players set #check i 0
				# Yeah, I'm lazy.
				execute on vehicle if entity @s[tag=vanilla_batter] as @n[tag=pan,distance=..6] run function pan:fill_batter {flavor:"vanilla"}
				execute on vehicle if entity @s[tag=chocolate_batter] as @n[tag=pan,distance=..6] run function pan:fill_batter {flavor:"chocolate"}
				execute on vehicle if entity @s[tag=berry_batter] as @n[tag=pan,distance=..6] run function pan:fill_batter {flavor:"strawberry"}
				execute as @n[tag=pan,distance=..6] run {
					scoreboard players set #check i 1
					say "Fill pan success!"
					function mixer:clear_batter_tags
				}
				execute if score #check i matches 0 run {
					say "Fill pan failed!"
					function mixer:clear_batter_tags
				}
			}
		}
	}
}