function on_load minecraft:load {
	# -1 = animating
	# 0 = unsquished
	# 1 = squished
	scoreboard players set #static_pipette i 0
}

function summon {
	function static_pipette:remove
	data remove storage map:frosting data.frosting
	execute positioned 34 6 20.0 rotated 180 0 run function animated_java:static_pipette/summon {args:{}}
	fill 33 6 16 35 6 21 barrier
}

function remove {
	function animated_java:static_pipette/remove/entities
	fill 33 6 16 35 6 21 air
}

function on_tick minecraft:tick {
	execute positioned 34 6 20.0 run {
		execute align xyz positioned ~-1 ~1 ~-3 as @e[type=minecraft:armor_stand,tag=ingredient,dx=2,dy=0,dz=4] run {
			execute if entity @s[tag=ingredient_vanilla_frosting] run return run {
				function util:kill_stacked_entities
				function static_pipette:summon
				function static_pipette:set_frosting {frosting:vanilla}
			}
			execute if entity @s[tag=ingredient_chocolate_frosting] run return run {
				function util:kill_stacked_entities
				function static_pipette:summon
				function static_pipette:set_frosting {frosting:chocolate}
			}
			execute if entity @s[tag=ingredient_strawberry_frosting] run return run {
				function util:kill_stacked_entities
				function static_pipette:summon
				function static_pipette:set_frosting {frosting:strawberry}
			}
		}
	}

	execute as @e[type=minecraft:item_display,tag=static_pipette,limit=1] at @s run {
		execute align xyz positioned ~-1 ~1 ~-3 run {
			execute if score #static_pipette i matches 0 if entity @a[dx=2,dy=0,dz=4] run return run {
				function animated_java:static_pipette/animations/squish/play
				playsound minecraft:entity.puffer_fish.flop block @a ~ ~ ~ 1 0.1
				execute as @n[tag=static_pipette_lock_point,distance=..20] at @s on passengers if entity @s[tag=pan] run function pan:frost with storage map:frosting data
				scoreboard players set #static_pipette i -1
			}
			execute if score #static_pipette i matches 1 unless entity @a[dx=2,dy=0,dz=4] run return run {
				function animated_java:static_pipette/animations/unsquish/play
				scoreboard players set #static_pipette i -1
			}
		}
	}
}

function create_lock_point {
	execute align xyz positioned ~.5 ~ ~.5 summon minecraft:item_display run {
		tag @s add pan_lock_point
		tag @s add static_pipette_lock_point
	}
}

function set_frosting {
	#ARGS: {frosting: string}
	$data modify storage map:frosting data.frosting set value '$(frosting)'
}