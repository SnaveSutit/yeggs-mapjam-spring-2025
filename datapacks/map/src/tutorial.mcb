import ./sequence.mcbt

function summon_marker {
	function animated_java:tutorial_indicator/summon {args:{animation:idle,start_animation:true}}
	execute as @e[tag=aj.tutorial_indicator.entity] run data merge entity @s {brightness:{sky:15,block:15},Glowing:true}
	playsound minecraft:block.note_block.bell block @a ~ ~ ~ 10 2
}

function remove_all_markers {
	function animated_java:tutorial_indicator/remove/entities
}

function start {
	function map:reset
	schedule 1t {
		function animated_java:troll/remove/all
		REPEAT(1, 10) as i {
			schedule clear tutorial:steps/<%i%>
		}

		function *msg {text:[ \
			'Welcome to ', \
			{text:'Pocket-sized Pantry ',color:green}, \
			{text:Panic,color:light_purple,italic:true,bold:true}, \
			'!', \
			{text:'\n\n           Created by ',color:gray}, \
			{text:'\n    SnaveSutit',color:red}, \
			" & ", \
			{text:'XelaFella',color:green}, \
			{text:"\n         as ",color:gray,italic:true}, \
			{text:'Team ADHD',color:blue,bold:true}, \
			{text:'\n\nCreated during the 96 hour Yeggs Spring MapJam of 2025!',color:dark_gray,italic:true}, \
		]}

		sequence 60t {
			function *msg {text:['This short tutorial will show you how to use the cake-making machines to feed ',{text:'His Immenseness, William Cornelius Grumblethorn III',color:green},'!']}
		}
		sequence 60t {
			function *msg {text:['Let\'s start by learning to drive the forklift!']}
		}
		sequence 40t {
			function *msg {text:['Head to the forklift, and right click it to get in the drivers seat!',{text:'\nLook for the big, yellow exclamation mark if you can\'t find it!',italic:true,color:gray}]}
			execute as @e[tag=forklift] at @s positioned ~ ~5 ~ run function tutorial:summon_marker
			function tutorial:steps/1
		}
	}
}

dir steps {
	function 1 {
		# Any player enters forklift
		scoreboard players set #check i 0
		execute as @a on vehicle if entity @s[tag=forklift_seat] run scoreboard players set #check i 1
		execute if score #check i matches 0 run return run schedule function ^0 1t

		function tutorial:remove_all_markers
		sequence reset
		tellraw @a '\n\n\n'
		function *msg {text:['Great! While riding the forklift, you can move using WASD']}
		sequence 60t {
			function *msg {text:['It can also be used to pick up and move ', {text:'Cake Pans',color:gold}, ' via punching, or interacting while the forks are near one. The forks will automatically adjust as needed.']}
		}
		sequence 80t {
			function *msg {text:['Let\'s try picking one up now! Head to the highlighted ', {text:'Cake Pan',color:gold}, ' and pick it up!']}
			execute positioned -19 6 -34 run function tutorial:summon_marker
			function tutorial:steps/2
		}
	}

	function 2 {
		# Any forklift picks up a cake pan
		scoreboard players set #check i 0
		execute as @e[tag=cake_mount] on passengers if entity @s[tag=pan] run scoreboard players set #check i 1
		execute if score #check i matches 0 run return run schedule function ^0 1t

		function tutorial:remove_all_markers
		sequence reset
		tellraw @a '\n\n\n'
		function *msg {text:['Excellent work! Now we can take this ', {text:'Cake Pan',color:gold}, ' to one of the ', {text:'Mixers',color:gold}, ' and fill it up with batter!']}
		sequence 60t {
			function *msg {text:['Head to the highlighted ', {text:'Mixer',color:gold}, ' and park the pan - still attached to the forklift - underneath it\'s spout!']}
			function tutorial:steps/3
		}
	}

	function 3 {
		# Any cake pan under a mixer's spout
		scoreboard players set #check i 0
		execute as @e[type=minecraft:item_display,tag=aj.mixer.root] at @s positioned ^ ^-4 ^9 run {
			particle end_rod ~ ~ ~
			execute if entity @e[type=minecraft:armor_stand,tag=pan,distance=..6] run scoreboard players set #check i 1
		}
		execute if score #check i matches 0 run return run schedule function ^0 1t

		function tutorial:remove_all_markers
		sequence reset
		tellraw @a '\n\n\n'
		function *msg {text:['Great! Go ahead and hop out of the forklift for now, because it\'s time to mix some batter!']}
		sequence 60t {
			function *msg {text:['First, we\'ll need to gather the necessary ingredients:',{text:'\n    - 1x Egg',color:gold},{text:'\n    - 1x Milk',color:gold},{text:'\n    - 1x Sugar',color:gold},{text:'\n    - 1x Flour',color:gold},{text:'\n    - 1x Flavor Ingredient (Vanilla, Chocolate, or Strawberry)',color:gold}]}
		}
		sequence 60t {
			function *msg {text:['You can find these ingredients on the ', {text:'Storage Shelf',color:gold}, '.']}
			execute positioned 11 11 -32 run function tutorial:summon_marker
		}
		sequence 20t {
			function *msg {text:['However, keep in mind you are very small, so you won\'t be able to pick up more than one ingredient at a time!']}
		}
		sequence 60t {
			function *msg {text:['You can pick up the ingredients by right clicking them, or by punching them, and drop them by sneaking, or throw them by right clicking or punching the ingredient!']}
		}
		sequence 80t {
			function *msg {text:['Transport the ingredients from the shelf to one of the highlighted ', {text:'Mixers',color:gold}, ' and chuck them into it\'s mixing bowl.',[{text:'\nYou can check the badges on top of the ',color:gray,italic:true},{text:'Mixers',color:gold}, ' to see which ingredients are missing!']]}
			execute as @e[tag=mixer] at @s positioned ~ ~14 ~ run function tutorial:summon_marker
			function tutorial:steps/4
		}
	}

	function 4 {
		# Any mixer is ready to mix
		execute unless entity @e[tag=mixer,scores={mixer_state=3}] run return run schedule function ^0 1t

		function tutorial:remove_all_markers
		sequence reset
		tellraw @a '\n\n\n'
		function *msg {text:['Wonderful! Now that we have all the ingredients, we can start mixing!']}
		sequence 60t {
			function *msg {text:['Simply walk into the handle on top of the ',{text:'Mixer',color:gold},' to start mixing!']}
			execute at @n[tag=mixer,scores={mixer_state=3}] at @s positioned ~ ~15 ~ run function tutorial:summon_marker
			function tutorial:steps/5
		}
	}

	function 5 {
		# Any mixer is ready to pour
		execute unless entity @e[tag=mixer,scores={mixer_state=4}] run return run schedule function ^0 1t

		function tutorial:remove_all_markers
		sequence reset
		tellraw @a '\n\n\n'
		function *msg {text:['Amazing! Now that the batter is mixed, we can pour it into the cake pan!']}
		sequence 40t {
			function *msg {text:['Head over to the valve on top of the ',{text:'Mixer',color:gold},' and right click it to pour the batter into the pan!']}
			execute as @e[tag=mixer,scores={mixer_state=4}] at @s positioned ^ ^14 ^9 run function tutorial:summon_marker
			function tutorial:steps/6
		}
	}

	function 6 {
		
	}
}

function msg {
	#ARGS: {text: JSON}
	$tellraw @a ['\n<',{text:Tutorial,color:aqua},'> ',$(text)]
	execute as @a at @s run playsound minecraft:block.wooden_pressure_plate.click_on block @s ~ ~ ~ 2 2
}