function on_load minecraft:load {
	scoreboard objectives add cook_time dummy
}

function summon {
	execute positioned ~ ~1000 ~ summon minecraft:armor_stand run {
		tag @s add pan
		tag @s add liftable_item
		tag @s add new
		data merge entity @s {Invisible:true,Silent:true}
		execute summon minecraft:interaction run {
			tag @s add pan_offset
			tag @s add new
			data merge entity @s {width:0, height:-1.95}
			ride @s mount @n[type=minecraft:armor_stand,tag=new,distance=..0.01]

			execute rotated 0 0 run function animated_java:pan/summon {args:{animation:"empty",variant:"empty"}}
			execute as @n[type=minecraft:item_display,tag=aj.pan.root,distance=..1] run {
				tag @s add pan_model
				ride @s mount @n[type=minecraft:interaction,tag=new,distance=..0.01]
			}
			tag @s remove new
		}
		execute summon minecraft:interaction run {
			tag @s add pan_offset
			tag @s add new
			data merge entity @s {width:0, height:-6}
			ride @s mount @n[type=minecraft:armor_stand,tag=new,distance=..0.01]

			execute summon minecraft:pig run {
				tag @s add pan_hitbox
				data merge entity @s {NoAI:true,Silent:true}
				attribute @s minecraft:scale base set 6
				attribute @s minecraft:movement_speed base set 0
				attribute @s minecraft:max_health base set 1
				effect give @s minecraft:invisibility infinite 1 true
				ride @s mount @n[type=minecraft:interaction,tag=new,distance=..0.01]
			}
			tag @s remove new
		}
		tag @s remove new
		tp @s ~ ~-1000 ~
	}
}


function on_tick minecraft:tick {
	execute as @e[type=minecraft:armor_stand,tag=pan] at @s run {
		# Check if the pan's hitbox is still there and is alive.
		scoreboard players set #check i 0
		execute on passengers on passengers if entity @s[type=minecraft:pig,nbt={DeathTime:0s}] run scoreboard players set #check i 1
		execute if score #check i matches 0 run {
			execute on passengers run {
				execute on passengers if entity @s[tag=aj.pan.root] run function animated_java:pan/remove/this
				kill @s
			}
			kill @s
			playsound minecraft:entity.item.break block @a ~ ~ ~ 2 0.9
		}

		# Attempt to attach to nearby lock points
		scoreboard players set #check i 0
		execute on vehicle run scoreboard players set #check i 1
		execute if score #check i matches 0 if entity @n[type=minecraft:item_display,tag=pan_lock_point,distance=..4] run {
			execute on passengers on passengers if entity @s[tag=aj.pan.root] run function pan:snap_rotation
			ride @s mount @n[type=minecraft:item_display,tag=pan_lock_point,distance=..4]
		}

		execute if entity @s[tag=burnt] run {
			particle minecraft:large_smoke ~ ~1 ~ 1.25 0 1.25 0 5
		}
	}
}


function snap_rotation {
	execute if entity @s[y_rotation=-45..45] run rotate @s 0 0
	execute if entity @s[y_rotation=135..-135] run rotate @s 180 0
	execute if entity @s[y_rotation=45..135] run rotate @s 90 0
	execute if entity @s[y_rotation=-135..-45] run rotate @s -90 0
}


function kill_all {
	kill @e[type=minecraft:pig,tag=pan_hitbox]
	kill @e[type=minecraft:interaction,tag=pan_offset]
	function animated_java:pan/remove/all
	kill @e[type=minecraft:armor_stand,tag=pan]
}


function fill_batter {
	#ARGS: {flavor: string}
	$execute on passengers on passengers if entity @s[tag=aj.pan.root] run { {flavor:$(flavor)}
		$data modify entity @s data.flavor set value $(flavor)
		$function animated_java:pan/variants/batter_$(flavor)/apply
		function animated_java:pan/animations/fill_batter/play
	}
}


function finish_baking {
	execute on passengers on passengers if entity @s[tag=aj.pan.root] run { with entity @s data
		$function animated_java:pan/variants/baked_$(flavor)/apply
		function animated_java:pan/animations/finish_baking/play
	}
}


function burn {
	tag @s add burnt
	execute on passengers on passengers if entity @s[tag=aj.pan.root] run {
		function animated_java:pan/variants/burnt/apply
	}
}


function empty_batter {
	function animated_java:pan/variants/empty
	function animated_java:pan/animations/empty/set_frame {frame:0}
}


function create_lock_point {
	execute align xyz positioned ~.5 ~ ~.5 summon minecraft:item_display run {
		tag @s add pan_lock_point
	}
}


function remove_lock_point {
	execute as @n[type=minecraft:item_display,tag=pan_lock_point] at @s run {
		particle minecraft:block{block_state:"redstone_block"} ~ ~ ~ 0 0 0 0 10
		kill @s
	}
}