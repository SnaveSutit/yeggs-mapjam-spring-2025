function on_load minecraft:load {
	scoreboard objectives add cook_time dummy
	scoreboard objectives add frosting_amount dummy
}

function summon {
	execute positioned ~ ~1000 ~ summon minecraft:armor_stand run {
		tag @s add pan
		tag @s add liftable_item
		scoreboard players set @s frosting_amount 0

		tag @s add new
		data merge entity @s {Invisible:true,Silent:true}
		execute summon minecraft:interaction run {
			tag @s add pan_offset
			tag @s add new
			data merge entity @s {width:0, height:-1.95}
			ride @s mount @n[type=minecraft:armor_stand,tag=new,distance=..0.01]

			execute rotated 0 0 run function animated_java:pan/summon {args:{animation:"empty",start_animation:true,variant:"empty"}}
			execute as @n[type=minecraft:item_display,tag=aj.pan.root,distance=..1] run {
				tag @s add pan_model
				execute on passengers unless entity @s[tag=aj.global.data] run data modify entity @s view_range set value 2
				ride @s mount @n[type=minecraft:interaction,tag=new,distance=..0.01]
			}
			tag @s remove new
		}
		execute summon minecraft:interaction run {
			tag @s add pan_offset
			tag @s add new
			data merge entity @s {width:0, height:-6}
			ride @s mount @n[type=minecraft:armor_stand,tag=new,distance=..0.01]

			execute summon minecraft:pig run {
				tag @s add pan_hitbox
				data merge entity @s {NoAI:true,Silent:true}
				attribute @s minecraft:scale base set 6
				attribute @s minecraft:movement_speed base set 0
				attribute @s minecraft:max_health base set 1
				effect give @s minecraft:invisibility infinite 1 true
				ride @s mount @n[type=minecraft:interaction,tag=new,distance=..0.01]
			}
			tag @s remove new
		}
		tag @s remove new
		tp @s ~ ~-1000 ~
	}
}


dir debug {
	REPEAT(['vanilla','chocolate','strawberry','lemon','carrot']) as batter {
		function summon_pan_batter_<%batter%> {
			function pan:summon
			execute as @n[tag=pan,distance=..1] run {
				function pan:fill_batter {batter:<%batter%>}
			}
		}
		function summon_pan_baked_<%batter%> {
			function pan:summon
			execute as @n[tag=pan,distance=..1] run {
				function pan:fill_batter {batter:<%batter%>}
				execute on passengers on passengers if entity @s[tag=aj.pan.root] run function animated_java:pan/animations/fill_batter/pause
				function pan:finish_baking
			}
		}
		REPEAT(['vanilla','chocolate','strawberry','lemon','carrot']) as frosting {
			function summon_pan_baked_<%batter%>_frosted_<%frosting%> {
				function pan:summon
				execute as @n[tag=pan,distance=..1] run {
					function pan:fill_batter {batter:<%batter%>}
					execute on passengers on passengers if entity @s[tag=aj.pan.root] run function animated_java:pan/animations/fill_batter/pause
					function pan:finish_baking
					function pan:frost {frosting:<%frosting%>}
					function pan:frost {frosting:<%frosting%>}
					function pan:frost {frosting:<%frosting%>}
					function pan:frost {frosting:<%frosting%>}
					function pan:frost {frosting:<%frosting%>}
					function pan:frost {frosting:<%frosting%>}
					function pan:frost {frosting:<%frosting%>}
				}
			}
		}
	}
}


function on_tick minecraft:tick {
	execute as @e[type=minecraft:item_display,tag=pan_dispenser] at @s unless entity @e[type=minecraft:armor_stand,tag=pan,distance=..5] run {
		scoreboard players remove @s spawn_timer 1
		execute if score @s spawn_timer matches ..0 run {
			function pan:summon
			scoreboard players set @s spawn_timer 120
		}
	}

	execute as @e[type=minecraft:armor_stand,tag=pan] at @s run {
		# Check if the pan's hitbox is still there and is alive.
		scoreboard players set #check i 0
		execute on passengers on passengers if entity @s[type=minecraft:pig,nbt={DeathTime:0s}] run scoreboard players set #check i 1
		execute if score #check i matches 0 run {
			execute on passengers run {
				execute on passengers if entity @s[tag=aj.pan.root] run function animated_java:pan/remove/this
				kill @s
			}
			kill @s
			playsound minecraft:entity.item.break block @a ~ ~ ~ 2 0.9
			execute positioned ~ 1 ~ rotated 0 0 run function animated_java:splat/summon {args:{animation:splat,start_animation:true}}
		}

		# Attempt to attach to nearby lock points
		scoreboard players set #check i 0
		execute on vehicle run scoreboard players set #check i 1
		execute if score #check i matches 0 if entity @n[type=minecraft:item_display,tag=pan_lock_point,distance=..4] run {
			scoreboard players set #has_passenger i 0
			execute as @n[type=minecraft:item_display,tag=pan_lock_point,distance=..4] on passengers run scoreboard players set #has_passenger i 1
			execute if score #has_passenger i matches 1 run return fail

			execute on passengers on passengers if entity @s[tag=aj.pan.root] run function util:snap_rotation
			ride @s mount @n[type=minecraft:item_display,tag=pan_lock_point,distance=..4]
		}

		execute if entity @s[tag=burnt] run {
			particle minecraft:large_smoke ~ ~1 ~ 1.25 0 1.25 0 5
		}
	}
}


function kill_all {
	kill @e[type=minecraft:pig,tag=pan_hitbox]
	kill @e[type=minecraft:interaction,tag=pan_offset]
	function animated_java:pan/remove/all
	kill @e[type=minecraft:armor_stand,tag=pan]
}


function fill_batter {
	#ARGS: {batter: string}
	tag @s add batter
	$execute on passengers on passengers if entity @s[tag=aj.pan.root] run { {batter:$(batter)}
		$data modify entity @s data.batter set value $(batter)
		$function animated_java:pan/variants/batter_$(batter)/apply
		function animated_java:pan/animations/fill_batter/play
	}
}


function finish_baking {
	execute if entity @s[tag=!batter] run return fail
	tag @s add baked
	execute on passengers on passengers if entity @s[tag=aj.pan.root] run { with entity @s data
		$function animated_java:pan/variants/baked_$(batter)/apply
		function animated_java:pan/animations/finish_baking/play
	}
}


function burn {
	tag @s add burnt
	execute on passengers on passengers if entity @s[tag=aj.pan.root] run {
		function animated_java:pan/variants/burnt/apply
	}
}


function frost {
	#ARGS: {frosting: string}
	execute if entity @s[tag=!batter] run return run tellraw @p {"text":"You need to mix and bake some batter first!","color":"red"}
	execute if entity @s[tag=!baked] run return run tellraw @p {"text":"You need to bake the batter first!","color":"red"}


	scoreboard players operation #this frosting_amount = @s frosting_amount
	$execute on passengers on passengers if entity @s[tag=aj.pan.root] run { {frosting:$(frosting)}

		$execute if score #this frosting_amount matches 1.. unless data entity @s {data:{frosting:$(frosting)}} run return run {
			say Cannot mix different frosting flavors!
		}

		$data modify entity @s data.frosting set value $(frosting)
		$function animated_java:pan/variants/frosted_$(frosting)/apply

		execute if score #this frosting_amount matches 6 run {
			function animated_java:pan/animations/frost3/play
			playsound minecraft:block.note_block.bell block @a ~ ~ ~ 2 1
			particle minecraft:happy_villager ~ ~3 ~ 2.5 0.25 2.5 1 20 force
			function static_pipette:remove
			execute on vehicle on vehicle run tag @s add frosted
		}
		execute if score #this frosting_amount matches 3 run {
			function animated_java:pan/animations/frost2/play
		}
		execute if score #this frosting_amount matches 0 run {
			function animated_java:pan/animations/frost1/play
		}
		scoreboard players add #this frosting_amount 1
	}
	scoreboard players operation @s frosting_amount = #this frosting_amount
}


function empty_batter {
	execute on passengers on passengers if entity @s[tag=aj.pan.root] run {
		function animated_java:pan/variants/empty/apply
		function animated_java:pan/animations/empty/set_frame {frame:0}
	}
}


function extract_to_layer {
	execute if entity @s[tag=!baked] run return run tellraw @p {"text":"You need to bake the batter first!","color":"red"}
	execute if entity @s[tag=!frosted] run return run tellraw @p {"text":"You need to frost the layer first!","color":"red"}
	data remove storage temp:temp data.batter
	execute on passengers on passengers if entity @s[tag=aj.pan.root] run {
		data remove storage temp:temp data
		data modify storage temp:temp data set from entity @s data
	}
	function layer:summon with storage temp:temp data
	function util:kill_stacked_entities
}


function create_lock_point {
	execute align xyz positioned ~.5 ~ ~.5 summon minecraft:item_display run {
		tag @s add pan_lock_point
	}
}


function remove_lock_point {
	execute as @n[type=minecraft:item_display,tag=pan_lock_point] at @s run {
		particle minecraft:block{block_state:"redstone_block"} ~ ~ ~ 0 0 0 0 10
		kill @s
	}
}

function create_dispenser {
	execute align xyz positioned ~.5 ~ ~.5 summon minecraft:item_display run {
		tag @s add pan_dispenser
	}
}